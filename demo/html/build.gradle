apply plugin: "java"
apply plugin: "jetty"

gwt {
    gwtVersion='2.8.2' // Should match the gwt version used for building the gwt backend
    maxHeapSize="1G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize="1G"

    src = files(file("src/")) // Needs to be in front of "modules" below.
    modules 'net.mgsx.gltf.demo.GdxDefinition'
    devModules 'net.mgsx.gltf.demo.GdxDefinitionSuperdev'
    project.webAppDirName = 'webapp'

    compiler {
        strict = true;
        disableCastChecking = true;
    }
}

task draftRun(type: JettyRunWar) {
    dependsOn draftWar
    dependsOn.remove('war')
    webApp=draftWar.archivePath
    daemon=true
}

task superDev(type: de.richsource.gradle.plugins.gwt.GwtSuperDev) {
    dependsOn draftRun
    doFirst {
    	gwt.modules = gwt.devModules
    }
}

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        file("build/dist").mkdirs()
        copy {
            from "build/gwt/out"
            into "build/dist"
            exclude 'WEB-INF', 'html/clear.cache.gif', 'html/html.devmode.js'
        }
        copy {
            from "webapp"
            into "build/dist"
            exclude 'WEB-INF'
        }
        copy {
            from "war"
            into "build/dist"
            exclude 'WEB-INF'
        }
    }
}

task validateDist(type: Exec /*, dependsOn: dist*/) {
	commandLine 'ruby', '-e', "require 'sinatra' ; set public_folder: '$buildDir/dist' ; set bind: '0.0.0.0'"
}

task distZip(type: Zip, /*, dependsOn: dist*/){
	from 'build/dist/'
   	archiveName "dist-${version}.zip"
   	destinationDir(file("build"))
}

draftWar {
   from "war"
}

task addSource << {
    sourceSets.main.compileClasspath += files(project(':gltf').sourceSets.main.allJava.srcDirs)
	sourceSets.main.compileClasspath += files(project(':demo:core').sourceSets.main.allJava.srcDirs)
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

sourceCompatibility = 1.6
sourceSets.main.java.srcDirs = [ "src/" ]


eclipse.project {
    name = appName + "-html"
}
